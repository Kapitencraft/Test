program     -> import* classDecl

declaration -> varDecl | statement

classDecl   -> "class" IDENTIFIER ( "extends" IDENTIFIER )? "{" (funcDecl | varDecl)* "}"
funcDecl    -> "def" VAR_TYPE function
varDecl     -> "final"? "static"? VAR_TYPE IDENTIFIER ( "=" expression )? ";"

statement   -> exprStmt | forStmt | ifStmt | printStmt | returnStmt | whileStmt | block
exprStmt    -> expression ";"
forStmt     -> "for" "(" ( varDecl | exprStmt)? ";" expression? ";" expression? ")" statement
ifStmt      -> "if" "(" expression ")" statement ( "elif" "(" expression ")" statement )* ( "else" statement )?
returnStmt  -> "return" expression? ";"
whileStmt   -> "while" "(" expression ")" statement
block       -> "{" declaration* "}"

expression  -> switch | when;

switch      -> "switch" "(" expression ")" "{" ( "case" primary "->" expression ";")* ("default" "->" expression ";")? "}"

when        -> cast_check ( "?" expression ":" expression)?

cast_check  -> assignment ( "instanceof" VAR_TYPE IDENTIFIER? )?

assignment  -> ( call "." )? IDENTIFIER "=" assignment | logic_or

logic_or    -> logic_and ( ("or" | "||") logic_and )*
logic_and   -> equality ( ("and" | "&&") equality )*
equality    -> comparison ( ( "!=" | "==" ) comparison )*
comparison  -> term ( ( ">" | ">=" | "<" | "<=" ) term )*
term        -> factor ( ( "-" | "+" ) factor )*
factor      -> unary ( ( "/" | "*" ) unary )*

unary       -> ( "!" | "-" ) unary | call | switch

call        -> primary ( "(" arguments? ")" | "." IDENTIFIER )*

primary     -> "true" | "false" | "null" | "this" | NUMBER | STRING | IDENTIFIER | "(" expression ")" | "super"

//util
function    -> IDENTIFIER "(" parameters? ")" block
parameters  -> VAR_TYPE IDENTIFIER ( "," IDENTIFIER )*
arguments   -> expression ( "," expression )*

//lexical
NUMBER      -> DIGIT+ ( "." DIGIT+ )?
STRING      -> "\"" <any char except "\"">* "\""
IDENTIFIER  -> ALPHA ( ALPHA | DIGIT )*
ALPHA       -> [a-zA-Z_]
DIGIT       -> [0-9]
VAR_TYPE    -> <ALPHA> #only registered